plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'maven-publish'
}

group 'org.abimon'
version = "${System.getenv('GIT_COMMIT') ?: "unknown"}".substring(0, 7)

ext {
    kotlin_version = '1.3.41'
    kotlin_coroutines_version = '1.3.0-M1'

    discord_version = '3.0.8'

    fuel_version = '2.2.0'

    h2_version = '1.4.199'
    jdbc_version = '5.1.38'
    hikari_version = '3.3.1'
    cloud_proxy_version = '1.0.4'

    jackson_version = '2.9.4'

    parboiled_version = '1.3.0'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter() //This prevents issues with transitive dependencies
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://maven.abimon.org"
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-reactor:$kotlin_coroutines_version"

    api "com.discord4j.discord4j:discord4j-core:$discord_version"

    api "com.github.kittinunf.fuel:fuel:$fuel_version"
    api "com.github.kittinunf.fuel:fuel-reactor:$fuel_version"
    api "com.github.kittinunf.fuel:fuel-coroutines:$fuel_version"

    api "com.h2database:h2:$h2_version"
    api "mysql:mysql-connector-java:$jdbc_version"
    api "com.zaxxer:HikariCP:$hikari_version"
    api "com.google.cloud.sql:mysql-socket-factory:$cloud_proxy_version"

    //api "org.abimon:Imperator:$imperator_build"
    //api "org.abimon:DiscordianArmada:$armada_build"

    api "org.parboiled:parboiled-java:$parboiled_version"
    api group: 'org.apache.commons', name: 'commons-pool2', version: '2.6.0'

    api "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    api "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    api "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jackson_version"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    api "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    api "com.fasterxml.jackson.module:jackson-module-parameter-names:$jackson_version"
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version"

    testApi group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    baseName = jar.baseName
    appendix = jar.appendix
    version = jar.version
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = jar.baseName
    appendix = jar.appendix
    version = jar.version
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url = "$buildDir/repo"
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}